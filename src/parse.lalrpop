// Copyright 2021 Kier Ada Davis
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//   http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

use crate::common::{Priority, Selector, TaskId};
use std::str::FromStr;

grammar;

pub Selector: Selector = {
  AllTwoOrMore,
  AnyTwoOrMore,
  Terms,
}

AllTwoOrMore: Selector = <head:Terms> AndOperator <tail:AllOneOrMore> => Selector::and(head, tail);
AllOneOrMore = { AllTwoOrMore, Terms };
AndOperator = { "&", "&&" };

AnyTwoOrMore: Selector = <head:Terms> OrOperator <tail:AnyOneOrMore> => Selector::or(head, tail);
AnyOneOrMore = { AnyTwoOrMore, Terms };
OrOperator = { "|", "||" };

Terms: Selector = {
  <head:Term> <tail:Terms> => Selector::and(head, tail),
  Term,
};

Term = {
  ":everything" => Selector::Everything,
  ":blocked" => Selector::Blocked,
  ":low" => Selector::Priority(Priority::Low),
  ":high" => Selector::Priority(Priority::High),
  ":urgent" => Selector::Priority(Priority::Urgent),
  TaskId => Selector::Id(<>),
  Label => Selector::Label(<>),
  Word => Selector::Word(<>),
  ":not" <Term> => Selector::not(<>),
  "(" <Selector> ")",
};

TaskId: TaskId = r":[0-9]+" => TaskId::from_str(<>).unwrap();
Label: String = r"@\S*" => String::from(&(<>[1..]));
Word: String = r"[-\w]+" => String::from(<>);
