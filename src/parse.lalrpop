// Copyright 2021 Kier Ada Davis
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//   http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

use crate::common::{Priority, Selector, TaskId};
use std::str::FromStr;

grammar;

pub Selector: Selector = {
  ImplicitAllTwoOrMore => Selector::all(<>),
  AllTwoOrMore => Selector::all(<>),
  AnyTwoOrMore => Selector::any(<>),
  Term,
}

ImplicitAllTwoOrMore: Vec<Selector> =
  <head:Term> <tail:ImplicitAllOneOrMore> => {
    let mut tail = tail; tail.push(head); tail
  };
ImplicitAllOneOrMore = {
  ImplicitAllTwoOrMore,
  Term => vec![<>],
};

AllTwoOrMore: Vec<Selector> =
  <head:Term> ":and" <tail:AllOneOrMore> => {
    let mut tail = tail; tail.push(head); tail
  };
AllOneOrMore = {
  AllTwoOrMore,
  Term => vec![<>],
};

AnyTwoOrMore: Vec<Selector> =
  <head:Term> ":or" <tail:AnyOneOrMore> => {
    let mut tail = tail; tail.push(head); tail
  };
AnyOneOrMore = {
  AnyTwoOrMore,
  Term => vec![<>],
};

Term = {
  ":everything" => Selector::Everything,
  ":blocked" => Selector::Blocked,
  ":low" => Selector::Priority(Priority::Low),
  ":high" => Selector::Priority(Priority::High),
  ":urgent" => Selector::Priority(Priority::Urgent),
  TaskId => Selector::Id(<>),
  Label => Selector::Label(<>),
  Word => Selector::Word(<>),
  ":not" <Term> => Selector::not(<>),
  "(" <Selector> ")",
};

TaskId: TaskId = r":[0-9]+" => TaskId::from_str(<>).unwrap();
Label: String = r"@\S*" => String::from(&(<>[1..]));
Word: String = r"[^:@\s]\S*" => String::from(<>);
